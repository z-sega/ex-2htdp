;; The first three lines of this file were inserted by DrRacket. They record metadata
;; about the language level of this file in a form that our tools can easily process.
#reader(lib "htdp-beginner-abbr-reader.ss" "lang")((modname ex-198) (read-case-sensitive #t) (teachpacks ()) (htdp-settings #(#t constructor repeating-decimal #f #t none #f () #f)))
(require 2htdp/batch-io)


(define LOCATION "/usr/share/dict/words")

; A List-of-Strings is one of:n
; - '()
; - (cons String List-of-Strings)
(define sm-l (list "Albarn"
                   "allen"
                   "barn"
                   "bishop"
                   "Ken"
                   "vision"
                   "zed"
                   "Zebra"))

; A Dictionary is a List-of-Strings.
(define AS-LIST (read-lines LOCATION))

; A Letter is one of the following 1Strings:
; - "a"
; - ...
; - "z"
; or, equivalently, a member? of this list:
(define LETTERS
  (explode "abcdefghijklmnopqrstuvwxyz"))

; List-of-Letters is one of:
; - '()
; - (cons Letter List-of-Letter)


(define-struct LC [letter count])
; LC (short for Letter-Count) is a structure:
;   (make-LC Letter Number)
; *intepretation*
; (make-LC "a" 2) describes the statistic that
; connects "a" to 2. It is used to keep
; track of how many times a letter appears in
; a given List-of-Strings
(define lc-a (make-LC "a" 2))
(define lc-b (make-LC "b" 2))
(define lc-c (make-LC "c" 0))


; A List-of-LCs is one of:
; - (cons LC '())
; - (cons LC List-of-LC)
(define lc1 (list lc-a))
(define lc0 (list (make-LC "a" 0)
                  (make-LC "b" 0)
                  (make-LC "c" 0)
                  (make-LC "d" 0)
                  (make-LC "e" 0)
                  (make-LC "f" 0)
                  (make-LC "g" 0)
                  (make-LC "h" 0)
                  (make-LC "i" 0)
                  (make-LC "j" 0)
                  (make-LC "k" 0)
                  (make-LC "l" 0)
                  (make-LC "m" 0)
                  (make-LC "n" 0)
                  (make-LC "o" 0)
                  (make-LC "p" 0)
                  (make-LC "q" 0)
                  (make-LC "r" 0)
                  (make-LC "s" 0)
                  (make-LC "t" 0)
                  (make-LC "u" 0)
                  (make-LC "v" 0)
                  (make-LC "w" 0)
                  (make-LC "x" 0)
                  (make-LC "y" 0)
                  (make-LC "z" 0)))
(define lcx (list (make-LC "a" 1)
                  (make-LC "b" 2)
                  (make-LC "c" 0)
                  (make-LC "d" 0)
                  (make-LC "e" 0)
                  (make-LC "f" 0)
                  (make-LC "g" 0)
                  (make-LC "h" 0)
                  (make-LC "i" 0)
                  (make-LC "j" 0)
                  (make-LC "k" 0)
                  (make-LC "l" 0)
                  (make-LC "m" 0)
                  (make-LC "n" 0)
                  (make-LC "o" 0)
                  (make-LC "p" 0)
                  (make-LC "q" 0)
                  (make-LC "r" 0)
                  (make-LC "s" 0)
                  (make-LC "t" 0)
                  (make-LC "u" 0)
                  (make-LC "v" 1)
                  (make-LC "w" 0)
                  (make-LC "x" 0)
                  (make-LC "y" 0)
                  (make-LC "z" 1)))

;; ex-198
;; Design words-by-first-letter. The function consumes a
;; Dictionary and produces a list of Dictionarys, one per
;; Letter.
;;
;; Redesign most-frequesnt from ex-197 using this new
;; function. Call the new function most-frequent.v2. Once
;; you have completed the design, ensure that the two
;; functions compute the same result on your computer's
;; dictionary:
;;
;; (check-expect
;;   (most-frequent AS-LIST)
;;   (most-frequent.v2 AS-LIST))
;;
;; *Note on Design Choices*
;; For words-by-first-letter you have a choice for
;; dealing with the situation when the given dictionary
;; does not contain any words for some letter:
;;
;; - One alternative is to exclude the resulting empty
;; dictionaries from the overall result. Doing so simplifies
;; both the testing of the function and the design of
;; most-frequent.v2, but it also requires the design of an
;; auxiliary function.
;;
;; - The other one is to include '() as the result of
;; looking for words of a certain letter, even if there
;; aren't any. This alternative avoids the auxiliary function
;; needed for the first alternative but adds complexity to
;; the design of most-frequent.v2


; A List-of-Dictionary is one of:
; - '()
; - (cons Dictionary List-of-Dictionary)
(define lod1 (list (list "allen")
                   (list "barn"
                         "bishop")
                   (list "vision")
                   (list "zed")))
                         
; LCOrFalse is one of:
; - #f
; - LC


; Dictionary -> List-of-Dictionary
; produces a List-of-Dictionary from d
(check-expect (words-by-first-letter '()) '())
(check-expect (words-by-first-letter sm-l) lod1)
              
(define (words-by-first-letter d)
  (cond
    [(empty? d) '()]
    [else (words-by-first-letter* LETTERS d)]))

; List-of-Letter List-of-String -> List-of-Dictionary
; computes a Dictionary for the given lol and los
; *implementation*
; if there are words-matching-first-letter for a given letter
; -> add those words to the List-of-Dictionary
; -else-> move on with the rest letters and their matching words
(check-expect (words-by-first-letter* LETTERS '()) '())
(check-expect (words-by-first-letter* '() '()) '())
(check-expect (words-by-first-letter* LETTERS sm-l) lod1)

(define (words-by-first-letter* lol los)
  (cond
    [(empty? lol) '()]
    [else (if (cons? (words-matching-first-letter (first lol) los))
              (cons (words-matching-first-letter (first lol) los)
                    (words-by-first-letter* (rest lol) los))
              (words-by-first-letter* (rest lol) los))]))

; Letter List-of-String -> Dictionary
; computes a list of all strings from los starting with l
(check-expect (words-matching-first-letter "a" '()) '())
(check-expect (words-matching-first-letter "b" sm-l)
              (list "barn" "bishop"))

(define (words-matching-first-letter l los)
  (cond
    [(empty? los) '()]
    [else (if (starts-with? l (first los))
              (cons (first los) (words-matching-first-letter l (rest los)))
              (words-matching-first-letter l (rest los)))]))


; List-of-String -> LCOrFalse
; computes the LC for the letter that occurs the
; most often as the first one in the given los
; *implementation*
; picks the pair with the maximum count
(check-expect (most-frequent '()) #f)
(check-expect (most-frequent sm-l) (make-LC "b" 2))

(define (most-frequent los)
  (cond
    [(empty? los) #f]
    [else (max-letter-count-list
           (count-by-letter los))]))


; Dictionary -> LCOrFalse
; return the most frequently used first
; letter in lod
;(check-expect (most-frequent.v2 '()) (most-frequent '()))
(check-expect (most-frequent.v2 sm-l) (most-frequent sm-l))

(define (most-frequent.v2 los)
  (most-frequent.v2* (words-by-first-letter los)))

; List-of-Dictionary -> LCOrFalse
; return the most frequently used first
; letter in lod
;(check-expect (most-frequent.v2* '()) (most-frequent '()))
(check-expect (most-frequent.v2* (words-by-first-letter sm-l))
              (most-frequent sm-l))

(define (most-frequent.v2* lod)
  (cond
    [(empty? lod) (make-LC "" 0)]
    [else
     (max-letter-count (compute-letter-count (first lod))
                       (most-frequent.v2* (rest lod)))]))


; Dictionary* -> LCOrFalse
; compute LC for the given Dictionary
; *implementation*
; Since the Dictionary* is a List-of-Strings
; where all the first Letters are the same
; letter is (first (explode (first ...)
; count is (length ...)
(check-expect (compute-letter-count '()) #f)
(check-expect (compute-letter-count (list "barn" "bishop"))
              (make-LC "b" 2))
(check-expect (compute-letter-count (list "allen"))
              (make-LC "a" 1))

(define (compute-letter-count d)
  (cond
    [(empty? d) #f]
    [else (make-LC (first (explode (first d)))
                   (length d))]))


; A NEList-of-LC is one of:
; - (cons LC '())
; - (cons LC NEList-of-LC)


; NEList-of-LC -> LC
; returns the LC with the highest count
; from lolc
(check-expect (max-letter-count-list (list (make-LC "a" 4)))
              (make-LC "a" 4))
(check-expect (max-letter-count-list lcx) (make-LC "b" 2))

(define (max-letter-count-list lolc)
  (cond
    [(empty? (rest lolc)) (first lolc)]
    [else
     (max-letter-count (first lolc)
                       (max-letter-count-list (rest lolc)))]))


; LC LC -> LC
; returns the LC with the highest count
; from LCs a and b
(check-expect (max-letter-count (make-LC "a" 4)
                                (make-LC "b" 2))
              (make-LC "a" 4))
(check-expect (max-letter-count (make-LC "a" 2)
                                (make-LC "b" 8))
              (make-LC "b" 8))

(define (max-letter-count a b)
  (if (>= (LC-count a) (LC-count b))
      a
      b))


; List-of-String -> List-of-LC
; counts how often each letter is used as the
; first one of a word in the given los
(check-expect (count-by-letter '()) '())
(check-expect (count-by-letter sm-l) lcx)

(define (count-by-letter los)
  (cond
    [(empty? los) '()]
    [else (count-by-letter-for-letters LETTERS los)]))
  

; List-of-Letters List-of-Words -> List-of-LC
; given a lol and los, where lol is the list
; of letters to check with, compute a List-of-LC
; based on starts-with# for each lol in los
(check-expect (count-by-letter-for-letters
               (list "a" "b") sm-l)
              (list (make-LC "a" 1)
                    (make-LC "b" 2)))

(define (count-by-letter-for-letters lol los)
  (cond
    [(empty? lol) '()]
    [else (cons (starts-with-to-lc (first lol) los)
                (count-by-letter-for-letters (rest lol) los))]))
 

; Letter List-of-String -> LC
; counts how often the given letter is used as
; the first letter of a word los
(check-expect (starts-with-to-lc "a" sm-l) (make-LC "a" 1))
(check-expect (starts-with-to-lc "v" sm-l) (make-LC "v" 1))
(check-expect (starts-with-to-lc "c" sm-l) (make-LC "c" 0))

(define (starts-with-to-lc l los)
  (make-LC l (starts-with# l los)))


; Letter Dictionary -> Number
; counts the number of words in los that start with l
(check-expect (starts-with# "f" sm-l) 0)
(check-expect (starts-with# "z" sm-l) 1)
(check-expect (starts-with# "b" sm-l) 2)

(define (starts-with# l los)
  (cond
    [(empty? los) 0]
    [else (+ (if (starts-with? l (first los))
                 1
                 0)
             (starts-with# l (rest los)))]))


; Letter String -> Boolean
; #t if the string s begins with the letter l
(check-expect (starts-with? "z" "zebra") #t)
(check-expect (starts-with? "a" "zebra") #f)
(check-expect (starts-with? "z" "Zebra") #f)
(check-expect (starts-with? "" "zebra") #f)

(define (starts-with? l s)
  (string=? l
            (first (explode s))))


(most-frequent AS-LIST)
